; Angles in rad
i15=1

CLOSE

;################################################
;; \file
;; Define motion for 2 jack system
;;
;; Original Author: Ronaldo Mercado
;;
;; Defined axes:
;; - X (addr 6) = height of surface in EGUs, PIVOT away from J1
;; - Y (addr 7) = angle of the surface in i15 units
;;
;; Macros:
;; - COORD = 2  CS number, e.g. 2
;; - PLC = 17  PLC number, should be CS number+15, e.g. 17
;; - J1 = 1  Axisnum for Jack 1, e.g. 1
;; - J2 = 2  Axisnum for Jack 2, e.g. 2
;; - DIST = 100  Distance between 2 jacks when they are in the zero position
;; - PIVOT = 25  Distance from jack 1 to pivot point of the surface
;; - DEPTH = 10  Depth of the surface on the mount
;################################################

; Change to CS2
&2

; Set relevant axes to use kinematics
#1->I ; +ve blade
#2->I ; -ve blade

; These are set by motor_in_cs.template
#define J1MOVE P(4700+1)
#define J1MRES P(4800+1)
#define J1OFF  P(4900+1)
#define J2MOVE P(4700+2)
#define J2MRES P(4800+2)
#define J2OFF  P(4900+2)

;; This is the distance between the 2 jacks when they are in the zero position
#define DIST Q20
DIST = 100
;; This is the distance from jack 1 to pivot point of the surface
#define PIVOT Q21
PIVOT = 25
;; This is the depth of the surface on the mount
#define DEPTH Q22
DEPTH = 10

; Calculate height and angle from jack positions
#define J1POS (J1MRES*P1+J1OFF)
#define J2POS (J2MRES*P2+J2OFF)
#define HEIGHT Q7
#define ANGLE Q8
; Local variables
#define RATIO Q128
OPEN FORWARD
CLEAR
    ; this is the difference in height of the jacks divided by their distance apart
    RATIO=(J2POS-J1POS)/DIST
    ; this is the angle of the surface
    ANGLE=atan(RATIO)
    ; the height of the surface
    HEIGHT=J1POS+RATIO*PIVOT+DEPTH/cos(ANGLE)
CLOSE

; Calculate jack positions from height and angle
#define SURFACE Q228
#define TANTHETA Q229
#define J1POS Q230
#define J2POS Q231
OPEN INVERSE
CLEAR
    ; this is the bottom edge of the surface
    SURFACE=HEIGHT-DEPTH/cos(ANGLE)
    ; store tan theta
    TANTHETA=tan(ANGLE)
    ; work out the jack positions in EGUs
    J1POS = SURFACE-PIVOT*TANTHETA
    J2POS = SURFACE+(DIST-PIVOT)*TANTHETA
    ; then cts
    P1=(J1POS-J1OFF)/J1MRES
    P2=(J2POS-J2OFF)/J2MRES
CLOSE

; A PLC(sx+15) needs to be made to do position reporting
; Readbacks should be in &{axisnum}Q81..89
; As forward kinematic, but with Px = mx62/(Ix08*32)
#define J1POS (J1MRES*m162/(I108*32)+J1OFF)
#define J2POS (J2MRES*m262/(I208*32)+J2OFF)
#define HEIGHT Q87
#define ANGLE Q88
; Local variables
#define RATIO Q328
OPEN PLC17
CLEAR
    ADDRESS&2
    ; this is the difference in height of the jacks divided by their distance apart
    RATIO=(J2POS-J1POS)/DIST
    ; this is the angle of the surface
    ANGLE=atan(RATIO)
    ; the height of the surface
    HEIGHT=J1POS+RATIO*PIVOT+DEPTH/cos(ANGLE)
    ; If jack motor record did the last move, set demands = readbacks
    if (J1MOVE = 1)
    or (J2MOVE = 1)
        J1MOVE = 0
        J2MOVE = 0
        Q77 = Q87
        Q78 = Q88
    endif
CLOSE
ENABLE PLC17
CLOSE

;####################################################
; Generic program for moving any co-ordinate system
; Original Author: Tom Cobb
; Used variables: Q71..Q79, Q81..Q89
; You should do a $$$ or type the following the first time you load this prog.
; If you type the commands in, ignore any errors. If you $$$, PLC1 will do it
; automatically for you at startup:
;&16 DEFINE LOOKAHEAD 50,10
;&15 DEFINE LOOKAHEAD 50,10
;&14 DEFINE LOOKAHEAD 50,10
;&13 DEFINE LOOKAHEAD 50,10
;&12 DEFINE LOOKAHEAD 50,10
;&11 DEFINE LOOKAHEAD 50,10
;&10 DEFINE LOOKAHEAD 50,10
;&9 DEFINE LOOKAHEAD 50,10
;&8 DEFINE LOOKAHEAD 50,10
;&7 DEFINE LOOKAHEAD 50,10
;&6 DEFINE LOOKAHEAD 50,10
;&5 DEFINE LOOKAHEAD 50,10
;&4 DEFINE LOOKAHEAD 50,10
;&3 DEFINE LOOKAHEAD 50,10
;&2 DEFINE LOOKAHEAD 50,10
;####################################################

;This program defines the program needed to do a move using the motor record
;on a CS axis
;to do a move we need to do &{CS}R
;to stop a move we need to do &{CS}A
;demand values are in &{CS}Q71..79
;The following are set by the motor record:
;Isx87 = Acceleration
;Isx88 = S curve
;Isx89 = Feedrate

; Set some defaults for all CS axes
i5213,15,100=50   ; segmentation time (needed for lookahead)
i5220,15,100=50   ; lookahead length (needed to limit max velocity to max set in CS)
i5250,15,100=1    ; Enable kinematics

OPEN PROG 10
CLEAR
LINEAR
ABS
; need to think about the consequences of including all axes in frax
FRAX(A,B,C,U,V,W,X,Y,Z)
A(Q71)B(Q72)C(Q73)U(Q74)V(Q75)W(Q76)X(Q77)Y(Q78)Z(Q79)
DWELL0
CLOSE

