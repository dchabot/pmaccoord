CLOSE

;################################################
;; \file
;; Define motion for 2 slit blades
;;
;; Original Author: Tom Cobb
;;
;; Defined axes:
;; - X (addr 6) = centre in same EGUs as blade
;; - Y (addr 7) = gap in same EGUs as blade
;;
;; Macros:
;; - COORD = $(COORD)  CS number, e.g. 2
;; - PLC = $(PLC)  PLC number, should be CS number+15, e.g. 17
;; - BP = $(BP)  Axisnum for Blade+, e.g. 1
;; - BM = $(BM)  Axisnum for Blade-, e.g. 3
;################################################

; Change to CS$(COORD)
&$(COORD)

; Set relevant axes to use kinematics
#$(BP)->I ; +ve blade
#$(BM)->I ; -ve blade

; These are set by motor_in_cs.template
#define BPMOVE P(4700+$(BP))
#define BPMRES P(4800+$(BP))
#define BPOFF  P(4900+$(BP))
#define BMMOVE P(4700+$(BM))
#define BMMRES P(4800+$(BM))
#define BMOFF  P(4900+$(BM))

; Calculate gap and centre from blade positions
#define BPPOS (BPMRES*P$(BP)+BPOFF)
#define BMPOS (BMMRES*P$(BM)+BMOFF)
#define CENTRE Q7
#define GAP Q8
OPEN FORWARD
CLEAR
    CENTRE = (BPPOS + BMPOS)/2
    GAP = BPPOS - BMPOS
CLOSE

; Calculate blade positions in cts from gap and centre
#define BPPOS Q228
#define BMPOS Q229
OPEN INVERSE
CLEAR
    ; calculate in EGUs
    BPPOS = CENTRE + GAP/2
    BMPOS = CENTRE - GAP/2
    ; then in cts
    P$(BP) = (BPPOS - BPOFF)/BPMRES
    P$(BM) = (BMPOS - BMOFF)/BMMRES
CLOSE

; A PLC(sx+15) needs to be made to do position reporting
; Readbacks should be in &{axisnum}Q81..89
; As forward kinematic, but with Px = mx62/(Ix08*32)
#define BPPOS (BPMRES*m$(BP)62/(I$(BP)08*32)+BPOFF)
#define BMPOS (BMMRES*m$(BM)62/(I$(BM)08*32)+BMOFF)
#define CENTRE Q87
#define GAP Q88
OPEN PLC$(PLC)
CLEAR
    ADDRESS&$(COORD)
    CENTRE = (BPPOS + BMPOS)/2
    GAP = BPPOS - BMPOS
    ; If blade motor record did the last move, set demands = readbacks
    if (BPMOVE = 1)
    or (BMMOVE = 1)
        BPMOVE = 0
        BMMOVE = 0
        Q77 = Q87
        Q78 = Q88
    endif
CLOSE
ENABLE PLC$(PLC)
